#compdef serenity serenity.sh

_serenity() {
    local args
    args=(
        '1:command:->commands'
        '2:target:->targets'
        '3:toolchain:->toolchains'
        '*:: :->args'
    )

    local commands
    commands=(
        'help'
        'build'
        'install'
        'image'
        'run'
        'gdb'
        'test'
        'delete'
        'recreate'
        'rebuild'
        'kaddr2line'
        'addr2line'
        'rebuild-toolchain'
        'rebuild-world'
        'copy-src'
    )

    local targets
    targets=(
        'x86_64:Target x86_64 or $SERENITY_ARCH (default)'
        'aarch64:Target aarch64'
        'lagom:Target host machine'
    )

    local toolchains
    toolchains=(
        'GNU:Toolchain gcc or $SERENITY_TOOLCHAIN (default)'
        'Clang:Toolchain clang'
    )

    _arguments -C -S "$args[@]"

    local command
    command="$line[1]"

    local target
    target="$line[2]"

    local toolchain
    toolchain="$line[3]"

    case "$state" in
        commands)
            _describe 'command' commands
            ;;
        targets)
            case "$command" in
                install|image|copy-src|kaddr2line|rebuild-toolchain|rebuild-world)
                    # lagom target is not supported for these, remove from targets
                    targets[$targets[(i)lagom]]=()
                    ;;
                help)
                    # Help command has no targets.
                    targets=()
                    ;;
            esac
            _describe 'target' targets
            ;;
        toolchains)
            if [[ "$command" != help && "$target" != lagom ]]; then
                # Toolchain-dependent invocations.
                _describe 'toolchain' toolchains
            fi
            ;;
        args)
            ;;
    esac

    return 0
}

_serenity
