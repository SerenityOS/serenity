import("//Meta/gn/build/libs/pulse/enable.gni")

# TODO: AudioClient IPC files for serenity

shared_library("LibAudio") {
  include_dirs = [ "//Userland/Libraries" ]
  sources = [
    "FlacLoader.cpp",
    "GenericTypes.cpp",
    "Loader.cpp",
    "MP3Loader.cpp",
    "Metadata.cpp",
    "PlaybackStream.cpp",
    "QOALoader.cpp",
    "QOATypes.cpp",
    "SampleFormats.cpp",
    "UserSampleQueue.cpp",
    "VorbisComment.cpp",
    "WavLoader.cpp",
    "WavWriter.cpp",
  ]
  if (enable_pulseaudio) {
    sources += [
      "PlaybackStreamPulseAudio.cpp",
      "PulseAudioWrappers.cpp",
    ]
  }
  if (current_os == "mac") {
    sources += [ "PlaybackStreamAudioUnit.cpp" ]
    frameworks = [ "AudioUnit.framework" ]
  }
  deps = [
    "//AK",
    "//Meta/gn/build/libs/pulse",
    "//Userland/Libraries/LibCore",
    "//Userland/Libraries/LibCrypto",
    "//Userland/Libraries/LibIPC",
    "//Userland/Libraries/LibRIFF",
    "//Userland/Libraries/LibThreading",
    "//Userland/Libraries/LibUnicode",
  ]
  output_name = "audio"
}
