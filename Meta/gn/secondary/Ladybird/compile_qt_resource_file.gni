import("qt_install_prefix.gni")

template("compile_qt_resource_file") {
  action_foreach(target_name) {
    forward_variables_from(invoker, [ "sources" ])

    script = "//Meta/gn/build/invoke_process_with_args.py"

    outputs = [ "$target_gen_dir/rcc_{{source_name_part}}.cpp" ]
    depfile = "$target_gen_dir/rcc_{{source_name_part}}.cpp.d"
    args = [
      qt_install_libexec + "rcc",
      "-g",
      "cpp",
      "-d",
      rebase_path(target_gen_dir, root_build_dir) +
          "/rcc_{{source_name_part}}.cpp.d",
      "-o",
      rebase_path(target_gen_dir, root_build_dir) +
          "/rcc_{{source_name_part}}.cpp",
      "{{source}}",
    ]
  }
}
