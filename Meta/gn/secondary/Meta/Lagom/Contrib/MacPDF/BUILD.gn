import("//Meta/gn/build/mac/compile_xib_resources.gni")

assert(current_os == "mac", "This application is macOS specific")

group("MacPDF") {
  deps = [ ":MacPDF.app" ]
}

compile_xib_resources("pdf_nibs") {
  sources = [ "MainMenu.xib" ]
}

executable("MacPDF_executable") {
  include_dirs = [
    "//Userland/Libraries",
    "//Userland/Services",
  ]
  defines = [ "AK_DONT_REPLACE_STD" ]
  cflags_objcc = [ "-fobjc-arc" ]
  deps = [
    ":pdf_nibs",
    "//AK",
    "//Userland/Libraries/LibCore",
    "//Userland/Libraries/LibGfx",
    "//Userland/Libraries/LibPDF",
  ]
  frameworks = [
    "Cocoa.framework",
    "UniformTypeIdentifiers.framework",
  ]
  sources = [
    "AppDelegate.mm",
    "MacPDFDocument.mm",
    "MacPDFOutlineViewDataSource.mm",
    "MacPDFView.mm",
    "MacPDFWindowController.mm",
    "main.mm",
  ]
  output_name = "MacPDF"
}

bundle_data("pdf_bundle_info_plist") {
  sources = [ "Info.plist" ]
  outputs = [ "{{bundle_contents_dir}}/Info.plist" ]
}

bundle_data("pdf_bundle_executables") {
  public_deps = [ ":MacPDF_executable" ]
  sources = [ "$root_out_dir/bin/MacPDF" ]
  outputs = [ "{{bundle_executable_dir}}/{{source_file_part}}" ]
}

bundle_data("pdf_bundle_libs") {
  public_deps = [
    "//AK",
    "//Userland/Libraries/LibCompress",
    "//Userland/Libraries/LibCore",
    "//Userland/Libraries/LibCrypto",
    "//Userland/Libraries/LibFileSystem",
    "//Userland/Libraries/LibGfx",
    "//Userland/Libraries/LibIPC",
    "//Userland/Libraries/LibPDF",
    "//Userland/Libraries/LibRIFF",
    "//Userland/Libraries/LibTextCodec",
    "//Userland/Libraries/LibURL",
  ]
  sources = [
    "$root_out_dir/lib/liblagom-ak.dylib",
    "$root_out_dir/lib/liblagom-compress.dylib",
    "$root_out_dir/lib/liblagom-core-minimal.dylib",
    "$root_out_dir/lib/liblagom-core.dylib",
    "$root_out_dir/lib/liblagom-crypto.dylib",
    "$root_out_dir/lib/liblagom-filesystem.dylib",
    "$root_out_dir/lib/liblagom-gfx.dylib",
    "$root_out_dir/lib/liblagom-ipc.dylib",
    "$root_out_dir/lib/liblagom-pdf.dylib",
    "$root_out_dir/lib/liblagom-riff.dylib",
    "$root_out_dir/lib/liblagom-textcodec.dylib",
    "$root_out_dir/lib/liblagom-url.dylib",
  ]
  outputs = [ "{{bundle_contents_dir}}/lib/{{source_file_part}}" ]
}

bundle_data("pdf_bundle_nibs") {
  public_deps = [ ":pdf_nibs" ]
  sources = get_target_outputs(":pdf_nibs")
  outputs = [ "{{bundle_resources_dir}}" + "{{source_file_part}}" ]
}

bundle_data("pdf_resources") {
  # FIXME: Don't list directories here, list out the files directly
  sources = [ "//Base/res/fonts" ]
  outputs = [ "{{bundle_resources_dir}}/res/" + "{{source_file_part}}" ]
}

create_bundle("MacPDF.app") {
  product_type = "com.apple.product-type.application"

  bundle_root_dir = "$root_build_dir/$target_name"
  bundle_contents_dir = "$bundle_root_dir/Contents"
  bundle_resources_dir = "$bundle_contents_dir/Resources"
  bundle_executable_dir = "$bundle_contents_dir/MacOS"

  deps = [
    ":pdf_bundle_executables",
    ":pdf_bundle_info_plist",
    ":pdf_bundle_libs",
    ":pdf_bundle_nibs",
    ":pdf_resources",
  ]
}
