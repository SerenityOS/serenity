/*
 * Copyright (c) 2017, Oracle and/or its affiliates. All rights reserved.
 * DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
 *
 * This code is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 only, as
 * published by the Free Software Foundation.
 *
 * This code is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 * version 2 for more details (a copy is included in the LICENSE file that
 * accompanied this code).
 *
 * You should have received a copy of the GNU General Public License version
 * 2 along with this work; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 *
 * Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 * or visit www.oracle.com if you need additional information or have any
 * questions.
 *
 */

/*
 * @test
 * @bug 8154587
 * @summary Check that invokeinterface of clone() works properly where clone() is
 *          an abstract method in a superinterface and not overridden locally.
 * @compile AbstractNoClones.jasm
 * @run main AbstractNoClones
 */

// The below .jasm code implements the following java code:
//
// public class AbstractNoClones {
//
//     interface I1 {
//         Object clone();
//     }
//
//     interface I2 extends I1 { }
//
//     static class C implements I2 { }
//
//     static Object test(I2 i) { return i.clone(); }
//
//     public static void main(String[] args) {
//         try {
//             String s = (String)test(new C());
//             throw new RuntimeException("Expected IAE not thrown");
//         } catch (java.lang.IllegalAccessError e) {
//             if (!e.toString().contains("C.clone")) {
//                 throw new RuntimeException("Wrong IllegalAccessError exception thrown");
//             }
//         }
//     }
// }


interface  AbstractNoClones$I1 version 53:0 {

    public abstract Method clone:"()Ljava/lang/Object;";

    static abstract interface InnerClass I1=class AbstractNoClones$I1 of class AbstractNoClones;

} // end Class AbstractNoClones$I1


interface  AbstractNoClones$I2 implements AbstractNoClones$I1 version 53:0 {

    static abstract interface InnerClass I2=class AbstractNoClones$I2 of class AbstractNoClones;
    static abstract interface InnerClass I1=class AbstractNoClones$I1 of class AbstractNoClones;

} // end Class AbstractNoClones$I2


super class AbstractNoClones$C implements AbstractNoClones$I2 version 53:0 {

    Method "<init>":"()V" stack 1 locals 1 {
        aload_0;
        invokespecial    Method java/lang/Object."<init>":"()V";
        return;
    }

    static InnerClass C=class AbstractNoClones$C of class AbstractNoClones;
    static abstract interface InnerClass I2=class AbstractNoClones$I2 of class AbstractNoClones;

} // end Class AbstractNoClones$C


super public class AbstractNoClones version 53:0 {

    public Method "<init>":"()V" stack 1 locals 1 {
        aload_0;
        invokespecial    Method java/lang/Object."<init>":"()V";
        return;
    }

    static Method test:"(LAbstractNoClones$I2;)Ljava/lang/Object;" stack 1 locals 1 {
        aload_0;
        invokeinterface    InterfaceMethod AbstractNoClones$I2.clone:"()Ljava/lang/Object;",  1;
        areturn;
    }

    public static Method main:"([Ljava/lang/String;)V" stack 3 locals 2 {
        try t0;
        new    class AbstractNoClones$C;
        dup;
        invokespecial    Method AbstractNoClones$C."<init>":"()V";
        invokestatic    Method test:"(LAbstractNoClones$I2;)Ljava/lang/Object;";
        checkcast    class java/lang/String;
        astore_1;
        new    class java/lang/RuntimeException;
        dup;
        ldc    String "Expected IAE not thrown";
        invokespecial    Method java/lang/RuntimeException."<init>":"(Ljava/lang/String;)V";
        athrow;
        endtry t0;
        catch t0 java/lang/IllegalAccessError;
        stack_frame_type stack1;
        stack_map class java/lang/IllegalAccessError;
        astore_1;
        aload_1;
        invokevirtual    Method java/lang/IllegalAccessError.toString:"()Ljava/lang/String;";
        ldc    String "C.clone";
        invokevirtual    Method java/lang/String.contains:"(Ljava/lang/CharSequence;)Z";
        ifne    L47;
        new    class java/lang/RuntimeException;
        dup;
        ldc    String "Wrong IllegalAccessError exception thrown";
        invokespecial    Method java/lang/RuntimeException."<init>":"(Ljava/lang/String;)V";
        athrow;
      L47:    stack_frame_type same;
        return;
    }

    static InnerClass C=class AbstractNoClones$C of class AbstractNoClones;
    static abstract interface InnerClass I2=class AbstractNoClones$I2 of class AbstractNoClones;
    static abstract interface InnerClass I1=class AbstractNoClones$I1 of class AbstractNoClones;

} // end Class AbstractNoClones
